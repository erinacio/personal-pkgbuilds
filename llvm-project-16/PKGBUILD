pkgname=llvm-project-16
pkgdesc="LLVM project, version 16"
pkgver=16.0.6
_pkgtag=llvmorg-16.0.6
pkgrel=2
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
groups=('local')
depends=('libxml2' 'libedit' 'perl' 'python')
makedepends=('cmake' 'ninja' 'zlib' 'swig')
options=('staticlibs')
source=(https://github.com/llvm/llvm-project/releases/download/$_pkgtag/llvm-project-$pkgver.src.tar.xz
        D139536.patch::'https://reviews.llvm.org/D139536?download=true'
        compiler-rt-move-allocator-base.patch::https://github.com/llvm/llvm-project/commit/fb77ca05ffb4.patch
        file://libcxx-disable-libatomic.patch)
sha256sums=('ce5e71081d17ce9e86d7cbcfa28c4b04b9300f8fb7e78422b1feb6bc52c3028e'
            'cb4535f00344f7415a64067b08b86d4845a9dffeb05d4755a2bf4ad5ef10bb38'
            '389c46e8a64a69cfa0c72d5901c4494bf6ea3a16a826704d55a816f94dcc6e3a'
            'SKIP')

prepare() {
  cd "$srcdir/llvm-project-$pkgver.src"

  # ASan: move allocator base to avoid conflict with high-entropy ASLR for x86-64 Linux
  patch -Np1 -i ../compiler-rt-move-allocator-base.patch

  patch -Np1 -i ../D139536.patch
  patch -Np1 -i ../libcxx-disable-libatomic.patch
}

build() {
  mkdir -p "$srcdir/llvm-project-$pkgver.src/tools"
  ln -sf "$srcdir/llvm-project-$pkgver.src/clang-tools-extra" "$srcdir/llvm-project-$pkgver.src/tools/extra"

  mkdir -p "$srcdir/llvm-project-$pkgver.src/build"
  cd "$srcdir/llvm-project-$pkgver.src/build"

  cmake ../llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/llvm16 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="x86_64-pc-linux-gnu" \
    -DLLVM_ENABLE_PER_TARGET_RUNTIME_DIR=OFF \
    -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lld;lldb;polly" \
    -DLLVM_ENABLE_RUNTIMES="compiler-rt;libcxx;libcxxabi;libunwind" \
    -DLLVM_ENABLE_BINDINGS=OFF \
    -DLLVM_ENABLE_ZLIB=ON \
    -DCLANG_DEFAULT_LINKER=lld \
    -DCOMPILER_RT_DEFAULT_TARGET_ONLY=ON \
    -DCOMPILER_RT_EXCLUDE_ATOMIC_BUILTIN=OFF \
    -DCOMPILER_RT_USE_LLVM_UNWINDER=ON \
    -DLIBCXX_USE_COMPILER_RT=ON \
    -DLIBCXXABI_USE_COMPILER_RT=ON \
    -DLIBCXXABI_USE_LLVM_UNWINDER=ON \
    -DLIBUNWIND_USE_COMPILER_RT=ON \
    -G "Ninja"
  ninja
}

package() {
  cd "$srcdir/llvm-project-$pkgver.src/build"
  DESTDIR="$pkgdir/" ninja install
}
