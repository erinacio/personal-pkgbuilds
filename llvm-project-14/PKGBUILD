pkgname=llvm-project-14
pkgdesc="LLVM project, version 14"
pkgver=14.0.0rc4
_pkgtag=llvmorg-14.0.0-rc4
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
groups=('modified')
depends=('libxml2' 'libedit' 'perl' 'python')
makedepends=('cmake' 'ninja' 'zlib')
options=('staticlibs')
source=(https://github.com/llvm/llvm-project/releases/download/$_pkgtag/llvm-project-$pkgver.src.tar.xz
        https://github.com/llvm/llvm-project/releases/download/$_pkgtag/clang-tools-extra-$pkgver.src.tar.xz)
sha256sums=('ca833d5ca97b2d46053f6d2759fcd9ec3201201275e0c2119a4680356f5b0a15'
            '354b4270bfdc299c08862a144a968c84c65b99bc47f5d3eea50806e8046c419a')

build() {
  mkdir -p "$srcdir/llvm-project-$pkgver.src/tools"
  ln -sf "$srcdir/clang-tools-extra-$pkgver.src" "$srcdir/llvm-project-$pkgver.src/tools/extra"

  mkdir -p "$srcdir/llvm-project-$pkgver.src/build"
  cd "$srcdir/llvm-project-$pkgver.src/build"

  cmake ../llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr/lib/llvm-14 \
    -DCMAKE_C_FLAGS_RELEASE="${CFLAGS} ${CPPFLAGS}" \
    -DCMAKE_CXX_FLAGS_RELEASE="${CXXFLAGS} ${CPPFLAGS}" \
    -DLLVM_ENABLE_PROJECTS="clang;libcxx;libcxxabi;libunwind;lldb;compiler-rt;lld;polly;clang-tools-extra" \
    -DLLVM_ENABLE_BINDINGS=OFF \
    -G "Ninja"
  ninja
}

package() {
  cd "$srcdir/llvm-project-$pkgver.src/build"
  DESTDIR="$pkgdir/" ninja install
}
