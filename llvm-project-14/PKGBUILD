pkgname=llvm-project-14
pkgdesc="LLVM project, version 14"
pkgver=14.0.6
_pkgtag=llvmorg-14.0.6
pkgrel=2
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
groups=('modified')
depends=('libxml2' 'libedit' 'perl' 'python')
makedepends=('cmake' 'ninja' 'zlib')
options=('staticlibs')
source=(https://github.com/llvm/llvm-project/releases/download/$_pkgtag/llvm-project-$pkgver.src.tar.xz
        https://github.com/llvm/llvm-project/releases/download/$_pkgtag/clang-tools-extra-$pkgver.src.tar.xz
        https://github.com/llvm/llvm-project/commit/9cf13067cb5088626ba7ee1ec4c42ec59c7995a0.patch)
sha256sums=('8b3cfd7bc695bd6cea0f37f53f0981f34f87496e79e2529874fd03a2f9dd3a8a'
            '7cf3b8ff56c65c4d1eae3c56883fc4a6cbc3ff9f3a1530a74d66e45d27271866'
            '34ed866e313e4580130a50118a4410d36fa0159123982521b6ef049439fc32ad')

prepare() {
  cd "$srcdir/llvm-project-$pkgver.src"
  patch -Np1 -i ../9cf13067cb5088626ba7ee1ec4c42ec59c7995a0.patch
}

build() {
  mkdir -p "$srcdir/llvm-project-$pkgver.src/tools"
  ln -sf "$srcdir/clang-tools-extra-$pkgver.src" "$srcdir/llvm-project-$pkgver.src/tools/extra"

  mkdir -p "$srcdir/llvm-project-$pkgver.src/build"
  cd "$srcdir/llvm-project-$pkgver.src/build"

  cmake ../llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr/lib/llvm-14 \
    -DCMAKE_C_FLAGS_RELEASE="${CFLAGS} ${CPPFLAGS}" \
    -DCMAKE_CXX_FLAGS_RELEASE="${CXXFLAGS} ${CPPFLAGS}" \
    -DLLVM_ENABLE_PROJECTS="clang;libcxx;libcxxabi;libunwind;lldb;compiler-rt;lld;polly;clang-tools-extra" \
    -DLLVM_ENABLE_BINDINGS=OFF \
    -G "Ninja"
  ninja
}

package() {
  cd "$srcdir/llvm-project-$pkgver.src/build"
  DESTDIR="$pkgdir/" ninja install
}
