pkgname=llvm-project-17
pkgdesc="LLVM project, version 17"
pkgver=17.0.6
_pkgtag=llvmorg-17.0.6
pkgrel=3
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
groups=('local')
depends=('libxml2' 'libedit' 'perl' 'python')
makedepends=('cmake' 'ninja' 'zlib' 'swig')
options=('staticlibs')
source=(https://github.com/llvm/llvm-project/releases/download/$_pkgtag/llvm-project-$pkgver.src.tar.xz
        file://libcxx-disable-libatomic.patch
        file://clang-add-local-library-path.patch
        file://llvm-runtime-override-default.patch)
sha256sums=('58a8818c60e6627064f312dbf46c02d9949956558340938b71cf731ad8bc0813'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/llvm-project-$pkgver.src"
  patch -Np1 -i ../libcxx-disable-libatomic.patch
  patch -Np1 -i ../clang-add-local-library-path.patch
  # Passing options from llvm to runtimes is broken.
  patch -Np1 -i ../llvm-runtime-override-default.patch
}

build() {
  mkdir -p "$srcdir/llvm-project-$pkgver.src/tools"
  ln -sf "$srcdir/llvm-project-$pkgver.src/clang-tools-extra" "$srcdir/llvm-project-$pkgver.src/tools/extra"

  mkdir -p "$srcdir/llvm-project-$pkgver.src/build"
  cd "$srcdir/llvm-project-$pkgver.src/build"

  cmake ../llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/llvm17 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="x86_64-pc-linux-gnu" \
    -DLLVM_ENABLE_PER_TARGET_RUNTIME_DIR=OFF \
    -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lld;lldb;polly" \
    -DLLVM_ENABLE_RUNTIMES="compiler-rt;libcxx;libcxxabi;libunwind" \
    -DLLVM_ENABLE_BINDINGS=OFF \
    -DLLVM_ENABLE_ZLIB=ON \
    -DCLANG_DEFAULT_LINKER=lld \
    -G "Ninja"
  ninja
}

package() {
  cd "$srcdir/llvm-project-$pkgver.src/build"
  DESTDIR="$pkgdir/" ninja install
}
