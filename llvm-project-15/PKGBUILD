pkgname=llvm-project-15
pkgdesc="LLVM project, version 15"
pkgver=15.0.5
_pkgtag=llvmorg-15.0.5
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
groups=('modified')
depends=('libxml2' 'libedit' 'perl' 'python')
makedepends=('cmake' 'ninja' 'zlib')
options=('staticlibs')
source=(https://github.com/llvm/llvm-project/releases/download/$_pkgtag/llvm-project-$pkgver.src.tar.xz
        https://github.com/llvm/llvm-project/releases/download/$_pkgtag/clang-tools-extra-$pkgver.src.tar.xz
        file://libcxx-disable-libatomic.patch)
sha256sums=('9c4278a6b8884eb7f4ae7dfe3c8e5445019824885e47cfdf1392563c47316fd6'
            '211399a64cd78105cf032a6c31d9cec9a4de5148439b577752bab5b353d44669'
            'SKIP')

prepare() {
  cd "$srcdir/llvm-project-$pkgver.src"
  patch -Np1 -i ../libcxx-disable-libatomic.patch
}

build() {
  mkdir -p "$srcdir/llvm-project-$pkgver.src/tools"
  ln -sf "$srcdir/clang-tools-extra-$pkgver.src" "$srcdir/llvm-project-$pkgver.src/tools/extra"

  mkdir -p "$srcdir/llvm-project-$pkgver.src/build"
  cd "$srcdir/llvm-project-$pkgver.src/build"

  cmake ../llvm \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/llvm15 \
    -DCMAKE_C_FLAGS_RELEASE="${CFLAGS} ${CPPFLAGS}" \
    -DCMAKE_CXX_FLAGS_RELEASE="${CXXFLAGS} ${CPPFLAGS}" \
    -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lld;lldb;polly" \
    -DLLVM_ENABLE_RUNTIMES="compiler-rt;libcxx;libcxxabi;libunwind" \
    -DLLVM_ENABLE_BINDINGS=OFF \
    -DLLVM_ENABLE_ZLIB=ON \
    -DCLANG_DEFAULT_LINKER=lld \
    -DCOMPILER_RT_EXCLUDE_ATOMIC_BUILTIN=OFF \
    -DLIBCXX_USE_COMPILER_RT=ON \
    -DLIBCXX_INSTALL_INCLUDE_TARGET_DIR=/opt/llvm15/include/c++/v1 \
    -DLIBCXXABI_USE_COMPILER_RT=ON \
    -DLIBCXXABI_USE_LLVM_UNWINDER=ON \
    -DLIBUNWIND_USE_COMPILER_RT=ON \
    -G "Ninja"
  ninja
}

package() {
  cd "$srcdir/llvm-project-$pkgver.src/build"
  DESTDIR="$pkgdir/" ninja install
}
